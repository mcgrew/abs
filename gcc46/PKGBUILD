
# NOTE: libtool requires rebuilt with each new gcc version

pkgname=gcc46
_ver=4.6
pkgver=4.6.3
pkgrel=1
pkgdesc="The GNU Compiler Collection (4.6.x)"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org/"
license=('GPL' 'LGPL' 'custom')
depends=('binutils>=2.20.1' 'mpfr>=2.4.2-2' 'cloog' 'zlib' 'libelf')
options=('!libtool')
conflicts=('gcc41' 'gcc42' 'gcc43' 'gcc44' 'gcc45')
source=("http://www.mirrorservice.org/sites/sourceware.org/pub/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2"
        "gcc-hash-style-both.patch"
        "gcc_pure64.patch")
sha256sums=('e8f5853d4eec2f5ebaf8a72ae4d53c436aacf98153b2499f8635b48c4718a093'
            '3492332fa78b545ff46c2b5293d17c63c122be6f8f6fa4798864b7d4572b0024'
            '2d369cf93c6e15c3559c3560bce581e0ae5f1f34dc86bca013ac67ef1c1a9ff9')

build() {
  cd "$srcdir/gcc-$pkgver"

  # ============================= TEST SUITE ============================= #
  #aria2c -k1M -V testsuite.metalink
  #tar xf gcc-testsuite.tar.bz2
  # ============================= TEST SUITE ============================= #


  #"Add" ppl-0.11 compatibility
  #sed -i "/ppl_minor_version=/s#10#11#" configure

  # Do not install libiberty
  sed -i -e 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  patch -Np0 -i "$srcdir/gcc-hash-style-both.patch"
  if test "$CARCH" == "x86_64"; then
    patch -Np1 -i "$srcdir/gcc_pure64.patch"
  fi
  echo "$pkgver" > gcc/BASE-VER
  rm -rf build
  mkdir build
  cd build
  ../configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --program-suffix=-4.6 \
    --enable-shared \
    --enable-languages=c,c++,fortran,objc,obj-c++ \
    --enable-__cxa_atexit \
    --disable-libstdcxx-pch \
    --disable-multilib \
    --disable-libgomp \
    --disable-libmudflap \
    --disable-libssp \
      --enable-clocale=gnu \
    --with-tune=generic \
    --with-ppl \
    --with-system-zlib
#    --with-cloog \
  make

  # ====================================================================== #
  # ============================= TEST SUITE ============================= #

  # increase stack size to prevent test failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=31827
  # do not abort on error as some are "expected"

  #cd ${srcdir}
  #ulimit -s 32768
  #make -k -j3 check || true
  #${_basedir}/contrib/test_summary

  # ============================= TEST SUITE ============================= #
  # ====================================================================== #
}

package() {
  cd "$srcdir/gcc-$pkgver/build"

  make DESTDIR="$pkgdir" install
  for f in "$pkgdir"/usr/share/info/*.info; do
    n=`basename "$f" | sed -e 's/\.info/-4.6.info/'`
    mv -f "$f" "$pkgdir/usr/share/info/$n"
  done
  rm -rf "$pkgdir/usr/share/man/man7"
  rm -rf "$pkgdir/usr/share/locale"
  mv "$pkgdir"/usr/lib/lib* \
     "$pkgdir/usr/lib/gcc/$CHOST/$pkgver/"
  # Install Runtime Library Exception
  install -Dm644 ../COPYING.RUNTIME \
    "$pkgdir/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION" \
  # Create links for gcc-4.6 build environment (useful for CUDA)
  mkdir -p "$pkgdir/opt/gcc-4.6"
  ln -s /usr/bin/gcc-4.6 "$pkgdir/opt/gcc-4.6/gcc"
  ln -s /usr/bin/g++-4.6 "$pkgdir/opt/gcc-4.6/g++"
}

