# Maintainer: M0Rf30

pkgname=cinnamon
pkgver=1.4UP3
pkgrel=2
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL')
depends=('accountsservice' 'caribou' 'dbus-glib' 'folks' 'gjs' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-menus2' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'libcroco' 'libgnomekbd' 'libpulse' 'metacity' 'muffin-wm' 'nautilus' 'networkmanager' 'notification-daemon' 'python2' 'python2-gconf' 'telepathy-logger')
makedepends=('git' 'gnome-common' 'intltool')
provides=('cinnamon')
conflicts=('cinnamon-git')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=(http://packages.linuxmint.com/pool/main/c/$pkgname/${pkgname}_1.4-UP3.tar.gz
#${pkgname}-1.4-UP3.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/1.4-UP3
fix-build-with-gnome-bluetooth-3.4.patch
fix-cinnamon-settings.patch)

build() {
  cd ${srcdir}
  cd Cinnamon

  patch -Np1 -i ../fix-build-with-gnome-bluetooth-3.4.patch
  patch -Np0 -i ../fix-cinnamon-settings.patch

  sed -i 's/-DG_DISABLE_DEPRECATED//g' src/Makefile.in browser-plugin/Makefile.in

  CFLAGS="${CFLAGS} -Wno-deprecated-declarations $(pkg-config --libs gmodule-2.0)"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon  \
               --localstatedir=/var --disable-static --disable-schemas-compile \
               --disable-schemas-install --enable-compile-warnings=yes
  make GLIB_COMPILE_SCHEMAS=glib-compile-schemas
}

package() {
  cd ${srcdir}
  cd Cinnamon
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  
  # Python2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/$pkgname-extension-tool" \
    "${pkgdir}/usr/lib/$pkgname-settings/$pkgname-settings.py" \
    "${pkgdir}/usr/bin/$pkgname-settings" \
    "${pkgdir}/usr/bin/$pkgname-menu-editor"
    
  # Kdm fix
  mkdir -p  $pkgdir/usr/share/apps/kdm/sessions/
  cp $pkgdir/usr/share/xsessions/$pkgname.desktop $pkgdir/usr/share/apps/kdm/sessions/
  
  # GConf2 stuff
  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" \
                    --domain cinnamon "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/"

}

md5sums=('3e0559999c1e8ac409104459b5e12b62'
         '50b40f7fef34dc7ca75bfe749e18d83c'
         'de3485c3b05856e7a210c37332281c7a')
