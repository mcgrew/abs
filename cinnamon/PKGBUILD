# Maintainer: M0Rf30

pkgname=cinnamon
pkgver=1.5.2
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL')
depends=('accountsservice' 'caribou' 'dbus-glib' 'folks' 'gjs' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'libcroco' 'libgnomekbd' 'libpulse' 'metacity' 'muffin-wm>=1.0.6' 'nautilus' 'networkmanager' 'notification-daemon' 'python2' 'python2-gconf' 'telepathy-logger' gnome-menus2)
makedepends=('gnome-common' 'intltool')
provides=('cinnamon')
conflicts=('cinnamon-git')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/$pkgver")

build() {
  cd ${srcdir}/linuxmint-Cinnamon*

 # Patches

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon  \
               --localstatedir=/var --disable-static --disable-schemas-compile \
               --disable-schemas-install --enable-compile-warnings=yes 
 
  make GLIB_COMPILE_SCHEMAS=glib-compile-schemas
}

package() {
  cd ${srcdir}/linuxmint-Cinnamon*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  
  # Python2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/$pkgname-extension-tool" \
    "${pkgdir}/usr/lib/$pkgname-settings/$pkgname-settings.py" \
    "${pkgdir}/usr/bin/$pkgname-settings" \
    "${pkgdir}/usr/bin/$pkgname-menu-editor"
    
  # Kdm fix
  mkdir -p  $pkgdir/usr/share/apps/kdm/sessions/
  cp $pkgdir/usr/share/xsessions/$pkgname.desktop $pkgdir/usr/share/apps/kdm/sessions/
}

md5sums=('7314b9cf730830c1fa3f9926f7c4272a')
