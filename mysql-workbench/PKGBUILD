# Maintainer: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mysql-workbench
epoch=1
pkgver=5.2.38
pkgrel=2
pkgdesc="A cross-platform, visual database design tool developed by MySQL"
arch=('i686' 'x86_64')
url="https://www.mysql.com/products/workbench/"
license=('GPL2')
depends=('libzip' 'libmysqlclient' 'lua' 'gtkmm' 'ctemplate' 'python2' 'libgnome-keyring' 'libgl' 'python-paramiko')
makedepends=('boost' 'curl' 'mesa')
options=('!libtool !distcc !ccache')
source=(ftp://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQLGUITools/${pkgname}-gpl-${pkgver}-src.tar.gz
        ArchLinux.xml arch.patch python27.patch gcc46.patch)
md5sums=('cd2a0cec9dffd5465b6999f5d9c8de78'
         'c2f986cd5737413d020c55db32713c48'
         '23430d24400fd65c9fd872bd8e2411e5'
         'a3efb373bd85d876530a29bf1780fb6b'
         '43917ebc79794ba15a64f7ba820fa20b')
sha256sums=('223fb450110bcb720c6393a1b7ab0769425f02612f3e0613163970705043e448'
            '5da4a499cfa5ecb4c6869305bc1f080b32c125627f45f1ce9cc9210567edd227'
            '09ed61a76b0f98bfc4652fe0bab34e2c9c399c0fb9e1481ccf408f07456d2058'
            'a7529405908427a31a391f40128a6f9386829cc26a55ababaadd71a71515a1db'
            '11e39be7d51a5af7b2940739e91e4d297af68bb3a1790e2eb0da4baddb662418')

build() {
  cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

  cp "${srcdir}/ArchLinux.xml" res/mysql.profiles
  patch -Np1 -i ${srcdir}/arch.patch

  patch -Np1 -i ${srcdir}/python27.patch
  patch -Np1 -i ${srcdir}/gcc46.patch

  # remove when http://bugs.mysql.com/bug.php?id=63898 gets fixed
  fgrep -rlZ pkglib_DATA --include Makefile.am . | xargs -0 sed -i 's/pkglib_DATA/pkgdata_DATA/g'

  ./autogen.sh
  ./configure --prefix=/usr

  make || return 1

  pushd ext/mysql-utilities
  python2 setup.py build
  popd

  pushd ext/connector-python
  python2 setup.py build
  popd
}

package() {
  cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

  make DESTDIR="${pkgdir}" install

  pushd ext/mysql-utilities
  python2 setup.py install --skip-profile --root $pkgdir --prefix=/usr --optimize=1
  popd

  pushd ext/connector-python
  python2 setup.py install --root $pkgdir --prefix=/usr --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      $(find ${pkgdir} -name '*.py')
}
