# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=lcms2
pkgname=lib32-${_pkgbase}
pkgver=2.3
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32 bit)"
arch=('x86_64')
license=('MIT')
depends=('lib32-glibc' "${_pkgbase}") # 'libtiff>=3.9.4'
url="http://www.littlecms.com"
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/lcms/${_pkgbase}-${pkgver}.tar.gz)
sha1sums=('67d5fabda2f5777ca8387766539b9c871d993133')

build() {
  export CC='gcc -m32'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32  \
  	--without-jpeg --without-tiff # these would be dependencies for binaries
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
