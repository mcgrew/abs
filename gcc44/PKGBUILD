# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Carson Reynolds <carson@k2.t.u-tokyo.ac.jp>
# Contributor: Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>

pkgname=gcc44
pkgver=4.4.6
pkgrel=1
pkgdesc="The GNU Compiler Collection (4.4.x)"
arch=('x86_64' 'i686')
url="http://gcc.gnu.org/"
license=('GPL' 'LGPL' 'custom')
depends=('binutils>=2.20.1' 'mpfr>=2.4.2-2' 'cloog' 'zlib' 'libelf')
options=('!libtool')
source=("http://www.mirrorservice.org/sites/sourceware.org/pub/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2"
        "gcc-hash-style-both.patch"
        "gcc_pure64.patch")
sha256sums=('57c718899e19277cbce9df3b0940a319e63c4630df6134185a45f157f24e8a0b'
            'a600550d3d2b2fb8ee6a547c68c3a08a2af7579290b340c35ee5598c9bb305a5'
            '2d369cf93c6e15c3559c3560bce581e0ae5f1f34dc86bca013ac67ef1c1a9ff9')

build() {
  cd "$srcdir/gcc-$pkgver"

  # Do not install libiberty
  sed -i -e 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  patch -Np0 -i "$srcdir/gcc-hash-style-both.patch"
  if test "$CARCH" == "x86_64"; then
    patch -Np1 -i "$srcdir/gcc_pure64.patch"
  fi
  echo "$pkgver" > gcc/BASE-VER
  rm -rf build
  mkdir build
  cd build
  ../configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --program-suffix=-4.4 \
    --enable-shared \
    --enable-languages=c,c++,fortran,objc,obj-c++ \
    --enable-__cxa_atexit \
    --disable-libstdcxx-pch \
    --disable-multilib \
    --disable-libgomp \
    --disable-libmudflap \
    --disable-libssp \
    --enable-clocale=gnu \
    --with-tune=generic \
    --with-cloog \
    --with-ppl \
    --with-system-zlib
  make
}

package() {
  cd "$srcdir/gcc-$pkgver/build"

  make DESTDIR="$pkgdir" install
  for f in "$pkgdir"/usr/share/info/*.info; do
    n=`basename "$f" | sed -e 's/\.info/-4.4.info/'`
    mv -f "$f" "$pkgdir/usr/share/info/$n"
  done
  rm -rf "$pkgdir/usr/share/man/man7"
  rm -rf "$pkgdir/usr/share/locale"
  mv "$pkgdir"/usr/lib/lib* \
     "$pkgdir/usr/lib/gcc/$CHOST/$pkgver/"
  # Install Runtime Library Exception
  install -Dm644 ../COPYING.RUNTIME \
    "$pkgdir/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION" \
  # Create links for gcc-4.4 build environment (useful for CUDA)
  mkdir -p "$pkgdir/opt/gcc-4.4"
  ln -s /usr/bin/gcc-4.4 "$pkgdir/opt/gcc-4.4/gcc"
  ln -s /usr/bin/g++-4.4 "$pkgdir/opt/gcc-4.4/g++"
}

# vim:set ts=2 sw=2 et:
